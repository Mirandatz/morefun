start       : _NL* rule+
rule        : NONTERMINAL ":" (layer | block)
NONTERMINAL : NAME

block        : block_option ("|" block_option)*
block_option : maybe_merge symbol_range+ maybe_fork _NL*
maybe_merge  : [MERGE]
maybe_fork   : [FORK]

symbol_range : NONTERMINAL ["~" RANGE_BOUND [".." RANGE_BOUND]]
RANGE_BOUND  : INT

layer        : (conv_layer | max_pooling_layer | avg_pooling_layer | batchnorm_layer | activation_layer) _NL*

activation_layer : RELU | GELU | SWISH

conv_layer   : CONV2D filter_count kernel_size strides
filter_count : FILTER_COUNT _int_args
kernel_size  : KERNEL_SIZE _int_args
strides      : STRIDE _int_args

batchnorm_layer : BATCHNORM

max_pooling_layer : MAX_POOL2D pool_sizes strides
avg_pooling_layer : AVG_POOL2D pool_sizes strides
pool_sizes        : POOL_SIZE _int_args

_int_args : INT_ARG
          | "(" INT_ARG ("|" INT_ARG)* ")"
INT_ARG   : INT

_float_args : FLOAT_ARG
            | "(" FLOAT_ARG ("|" FLOAT_ARG)* ")"
FLOAT_ARG   : FLOAT

RELU  : /"relu"/
GELU  : /"gelu"/
SWISH : /"swish"/

MERGE : /"merge"/
FORK  : /"fork"/

CONV2D       : /"conv2d"/
FILTER_COUNT : /"filter_count"/
KERNEL_SIZE  : /"kernel_size"/
STRIDE       : /"stride"/

BATCHNORM : /"batchnorm"/

MAX_POOL2D : /"max_pool2d"/
AVG_POOL2D : /"avg_pool2d"/
POOL_SIZE  : /"pool_size"/

%import common (INT, FLOAT)
%import common.NEWLINE -> _NL
%import common.CNAME -> NAME
%import common.WS_INLINE

%ignore WS_INLINE
