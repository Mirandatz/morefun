start: backbone optimizer

backbone : block+
block    : MERGE? layer FORK?
layer    : random_flip
         | random_rotation
         | random_translation
         | resizing
         | random_crop
         | dense
         | conv
         | maxpool
         | avgpool
         | batchnorm
         | activation

random_flip        : RANDOM_FLIP FLIP_MODE
random_rotation    : RANDOM_ROTATION QUOTED_FLOAT
random_translation : RANDOM_TRANSLATION QUOTED_FLOAT
resizing           : RESIZING height width
height             : HEIGHT QUOTED_INT
width              : WIDTH QUOTED_INT
random_crop        : RANDOM_CROP height width
height_factor      : HEIGHT_FACTOR QUOTED_FLOAT
width_factor       : WIDTH_FACTOR QUOTED_FLOAT

dense : DENSE QUOTED_INT

conv         : CONV filter_count kernel_size stride
filter_count : FILTER_COUNT QUOTED_INT
kernel_size  : KERNEL_SIZE QUOTED_INT
stride       : STRIDE QUOTED_INT

maxpool  : MAXPOOL pool_size stride
avgpool  : AVGPOOL pool_size stride
pool_size : POOL_SIZE QUOTED_INT
 
batchnorm : BATCHNORM

activation : RELU | GELU | PRELU | SWISH

optimizer: sgd | adam | ranger

sgd           : SGD learning_rate momentum nesterov
learning_rate : LEARNING_RATE QUOTED_FLOAT
momentum      : MOMENTUM QUOTED_FLOAT
nesterov      : NESTEROV BOOL

adam    : ADAM learning_rate beta1 beta2 epsilon amsgrad
beta1   : BETA1 QUOTED_FLOAT
beta2   : BETA2 QUOTED_FLOAT
epsilon : EPSILON QUOTED_FLOAT
amsgrad : AMSGRAD BOOL

ranger         : RANGER learning_rate beta1 beta2 epsilon amsgrad sync_period slow_step_size
sync_period    : SYNC_PERIOD QUOTED_INT
slow_step_size : SLOW_STEP_SIZE QUOTED_FLOAT

// using multiple imports just for aesthetic reasons
%import .terminals (DENSE)
%import .terminals (RANDOM_FLIP, FLIP_MODE, RANDOM_ROTATION, RANDOM_CROP)
%import .terminals (RESIZING, HEIGHT, WIDTH)
%import .terminals (RANDOM_TRANSLATION, HEIGHT_FACTOR, WIDTH_FACTOR)
%import .terminals (MERGE, FORK)
%import .terminals (RELU, GELU, PRELU, SWISH)
%import .terminals (CONV, FILTER_COUNT, KERNEL_SIZE, STRIDE)
%import .terminals (MAXPOOL, AVGPOOL, POOL_SIZE)
%import .terminals (BATCHNORM)
%import .terminals (SGD, LEARNING_RATE, MOMENTUM, NESTEROV)
%import .terminals (ADAM, BETA1, BETA2, EPSILON, AMSGRAD)
%import .terminals (RANGER, SYNC_PERIOD, SLOW_STEP_SIZE)
%import .terminals (BOOL, QUOTED_INT, QUOTED_FLOAT)

%import common.WS

%ignore WS
