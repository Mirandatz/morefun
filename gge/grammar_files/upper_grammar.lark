start       : _NL* rule+
rule        : NONTERMINAL ":" (layer_and_maybe_emptylines | block | optimizer_and_maybe_emptylines)
NONTERMINAL : NAME

block        : block_option ("|" block_option)*
block_option : maybe_merge symbol_range+ maybe_fork _NL*
maybe_merge  : [MERGE]
maybe_fork   : [FORK]

symbol_range : NONTERMINAL ["~" RANGE_BOUND [".." RANGE_BOUND]]
RANGE_BOUND  : INT

layer_and_maybe_emptylines : layer _NL*
layer : random_flip
      | random_rotation
      | random_translation
      | resizing
      | random_crop
      | conv_layer 
      | max_pooling_layer 
      | avg_pooling_layer 
      | batchnorm_layer 
      | activation_layer

random_flip : RANDOM_FLIP _flip_args

random_rotation : RANDOM_ROTATION _float_args

random_translation : RANDOM_TRANSLATION height_factor width_factor
height_factor      : HEIGHT_FACTOR _float_args
width_factor       : WIDTH_FACTOR _float_args

resizing : RESIZING height width
height  : HEIGHT _int_args
width   : WIDTH _int_args

random_crop : RANDOM_CROP height width

conv_layer   : CONV filter_count kernel_size strides
filter_count : FILTER_COUNT _int_args
kernel_size  : KERNEL_SIZE _int_args
strides      : STRIDE _int_args

batchnorm_layer : BATCHNORM

max_pooling_layer : MAXPOOL pool_sizes strides
avg_pooling_layer : AVGPOOL pool_sizes strides
pool_sizes        : POOL_SIZE _int_args

activation_layer : RELU | GELU | SWISH | PRELU

optimizer_and_maybe_emptylines : optimizer _NL*
optimizer : sgd | adam | ranger

sgd           : SGD learning_rate momentum nesterov
learning_rate : LEARNING_RATE _float_args
momentum      : MOMENTUM _float_args
nesterov      : NESTEROV _bool_args

adam : ADAM learning_rate beta1 beta2 epsilon amsgrad
beta1: BETA1 _float_args
beta2: BETA2 _float_args
epsilon: EPSILON _float_args
amsgrad: AMSGRAD _bool_args

ranger         : RANGER learning_rate beta1 beta2 epsilon amsgrad sync_period slow_step_size
sync_period    : SYNC_PERIOD _int_args
slow_step_size : SLOW_STEP_SIZE _float_args

_int_args : INT_ARG
          | "(" INT_ARG ("|" INT_ARG)* ")"
INT_ARG   : QUOTED_INT

_float_args : FLOAT_ARG
            | "(" FLOAT_ARG ("|" FLOAT_ARG)* ")"
FLOAT_ARG : QUOTED_FLOAT

_bool_args : BOOL_ARG
           | "(" BOOL_ARG ("|" BOOL_ARG)* ")"
BOOL_ARG   : BOOL

_flip_args  : FLIP_MODE 
            | "(" FLIP_MODE ("|" FLIP_MODE)* ")"

// using multiple imports just for aesthetic reasons
%import .terminals (RANDOM_FLIP, FLIP_MODE)
%import .terminals (RANDOM_ROTATION)
%import .terminals (RESIZING, HEIGHT, WIDTH)
%import .terminals (RANDOM_TRANSLATION, HEIGHT_FACTOR, WIDTH_FACTOR)
%import .terminals (RANDOM_CROP)
%import .terminals (MERGE, FORK)
%import .terminals (RELU, GELU, SWISH, PRELU)
%import .terminals (CONV, FILTER_COUNT, KERNEL_SIZE, STRIDE)
%import .terminals (MAXPOOL, AVGPOOL, POOL_SIZE)
%import .terminals (BATCHNORM)
%import .terminals (SGD, LEARNING_RATE, MOMENTUM, NESTEROV)
%import .terminals (ADAM, BETA1, BETA2, EPSILON, AMSGRAD)
%import .terminals (RANGER, SYNC_PERIOD, SLOW_STEP_SIZE)
%import .terminals (BOOL, QUOTED_INT, QUOTED_FLOAT)

%import common.NEWLINE -> _NL
%import common.CNAME -> NAME
%import common.WS_INLINE
%import common.INT

%ignore WS_INLINE
