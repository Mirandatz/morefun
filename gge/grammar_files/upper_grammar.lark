start       : _NL* rule+
rule        : NONTERMINAL ":" (data_augmentation | layer | block | optimizer)
NONTERMINAL : NAME

block        : block_option ("|" block_option)*
block_option : maybe_merge symbol_range+ maybe_fork _NL*
maybe_merge  : [MERGE]
maybe_fork   : [FORK]

symbol_range : NONTERMINAL ["~" RANGE_BOUND [".." RANGE_BOUND]]
RANGE_BOUND  : INT

data_augmentation : DATA_AUGMENTATION rotation width_shift height_shift zoom horizontal_flip vertical_flip _NL*
rotation          : ROTATION _float_args
width_shift       : WIDTH_SHIFT _float_args
height_shift      : HEIGHT_SHIFT _float_args
zoom              : ZOOM _float_args
horizontal_flip   : HORIZONTAL_FLIP _bool_args
vertical_flip     : VERTICAL_FLIP _bool_args

layer        : (conv_layer | max_pooling_layer | avg_pooling_layer | batchnorm_layer | activation_layer) _NL*

conv_layer   : CONV filter_count kernel_size strides
filter_count : FILTER_COUNT _int_args
kernel_size  : KERNEL_SIZE _int_args
strides      : STRIDE _int_args

batchnorm_layer : BATCHNORM

max_pooling_layer : MAXPOOL pool_sizes strides
avg_pooling_layer : AVGPOOL pool_sizes strides
pool_sizes        : POOL_SIZE _int_args

activation_layer : RELU | GELU | SWISH

optimizer : (sgd | adam) _NL*

sgd           : SGD learning_rate momentum nesterov
learning_rate : LEARNING_RATE _float_args
momentum      : MOMENTUM _float_args
nesterov      : NESTEROV _bool_args

adam : ADAM learning_rate beta1 beta2 epsilon amsgrad
beta1: BETA1 _float_args
beta2: BETA2 _float_args
epsilon: EPSILON _float_args
amsgrad: AMSGRAD _bool_args

_int_args : INT_ARG
          | "(" INT_ARG ("|" INT_ARG)* ")"
INT_ARG   : INT

_float_args : FLOAT_ARG
            | "(" FLOAT_ARG ("|" FLOAT_ARG)* ")"
FLOAT_ARG : FLOAT

_bool_args : BOOL_ARG
           | "(" BOOL_ARG ("|" BOOL_ARG)* ")"
BOOL_ARG   : BOOL

// using multiple imports just for aesthetic reasons
%import .terminals (DATA_AUGMENTATION, ROTATION, WIDTH_SHIFT, HEIGHT_SHIFT, ZOOM, HORIZONTAL_FLIP, VERTICAL_FLIP)
%import .terminals (MERGE, FORK)
%import .terminals (RELU, GELU, SWISH)
%import .terminals (CONV, FILTER_COUNT, KERNEL_SIZE, STRIDE)
%import .terminals (MAXPOOL, AVGPOOL, POOL_SIZE)
%import .terminals (BATCHNORM)
%import .terminals (SGD, LEARNING_RATE, MOMENTUM, NESTEROV)
%import .terminals (ADAM, BETA1, BETA2, EPSILON, AMSGRAD)
%import .terminals (BOOL)

%import common.INT
%import common.NUMBER -> FLOAT
%import common.NEWLINE -> _NL
%import common.CNAME -> NAME
%import common.WS_INLINE

%ignore WS_INLINE
